# A gnuplot file to plot Latency data sweeping over differnt windows sizes
#
# invoke with:
# gnuplot -e "base=<basefile name>" lat_cmd_sweep.gpl
#

data_file = sprintf("%s.dat", base)
out_file = sprintf("%s.pdf", base)
out_file_eb = sprintf("%s-eb.pdf", base)
out_file_avg = sprintf("%s-avg.pdf", base)

set terminal pdf color solid enhanced

set xlabel 'Host Offset (Bytes)'
set ylabel 'Latency (ns)'
set key top right

set xrange [-1:65]

set mytics 4
set grid ytics mytics xtics mxtics
set offsets graph 0, 0, 0.01, 0.01

set output out_file
plot \
     data_file index 0 using 4:17 w lp lw 2 t '8B 4KB Win Cold', \
     ''        index 2 using 4:17 w lp lw 2 t '8B 8MB Win Cold', \
     ''        index 4 using 4:17 w lp lw 2 t '64B 4KB Win Cold', \
     ''        index 6 using 4:17 w lp lw 2 t '64B 8MB Win Cold', \
     ''        index 1 using 4:17 w lp lw 2 t '8B 4KB Win HWarm', \
     ''        index 3 using 4:17 w lp lw 2 t '8B 8MB Win HWarm', \
     ''        index 5 using 4:17 w lp lw 2 t '64B 4KB Win HWarm', \
     ''        index 7 using 4:17 w lp lw 2 t '64B 8MB Win HWarm'

set output out_file_avg
plot \
     data_file index 0 using 4:16 w lp lw 2 t '8B 4KB Win Cold', \
     ''        index 2 using 4:16 w lp lw 2 t '8B 8MB Win Cold', \
     ''        index 4 using 4:16 w lp lw 2 t '64B 4KB Win Cold', \
     ''        index 6 using 4:16 w lp lw 2 t '64B 8MB Win Cold', \
     ''        index 1 using 4:16 w lp lw 2 t '8B 4KB Win HWarm', \
     ''        index 3 using 4:16 w lp lw 2 t '8B 8MB Win HWarm', \
     ''        index 5 using 4:16 w lp lw 2 t '64B 4KB Win HWarm', \
     ''        index 7 using 4:16 w lp lw 2 t '64B 8MB Win HWarm'

set output out_file_eb
plot \
     data_file index 0 using 4:17       w l  ls 1 lw 2 t '8B 4KB Win Cold', \
     ''        index 0 using 4:17:18:20 w ye ls 1 lw 2 t '', \
     ''        index 2 using 4:17       w l  ls 3 lw 2 t '8B 8MB Win Cold', \
     ''        index 2 using 4:17:18:20 w ye ls 3 lw 2 t '', \
     ''        index 4 using 4:17       w l  ls 5 lw 2 t '64B 4KB Win Cold', \
     ''        index 4 using 4:17:18:20 w ye ls 5 lw 2 t '', \
     ''        index 6 using 4:17       w l  ls 7 lw 2 t '64B 8MB Win Cold', \
     ''        index 6 using 4:17:18:20 w ye ls 7 lw 2 t '', \
     ''        index 1 using 4:17       w l  ls 2 lw 2 t '8B 4KB Win HWarm', \
     ''        index 1 using 4:17:18:20 w ye ls 2 lw 2 t '', \
     ''        index 3 using 4:17       w l  ls 4 lw 2 t '8B 8MB Win HWarm', \
     ''        index 3 using 4:17:18:20 w ye ls 4 lw 2 t '', \
     ''        index 5 using 4:17       w l  ls 6 lw 2 t '64B 4KB Win HWarm', \
     ''        index 5 using 4:17:18:20 w ye ls 6 lw 2 t '', \
     ''        index 7 using 4:17       w l  ls 8 lw 2 t '64B 8MB Win HWarm', \
     ''        index 7 using 4:17:18:20 w ye ls 8 lw 2 t ''
